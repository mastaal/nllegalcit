// Copyright 2023, Martijn Staal <nllegalcit [at] martijn-staal.nl>
// SPDX-License-Identifier: EUPL-1.2
// Available under the EUPL-1.2, or, at your option, any later version.
// Parser grammar for Kamerstuk citations
// This is essentially a (customized) Lark-translation from the (EUPL-1.2 licensed) Waxeye grammar used by
// KOOP, which can be found here: https://gitlab.com/koop/ld/lx/linkextractor/-/blob/main/src/webapp/links/grammars/oep-parl-docs.waxeye?

// TODO: Support for odd kamernrs and R nummers
kamerstuk: KSLABEL KSSEP kamer KSSEP vergaderjaar KSSEP dossiernummer KSSEP ONDERNUMMERNR KSSEP ondernummer KSSEP? paginaverwijzing?

KSLABEL: /\(?kamerstuk(ken)?/i

KSSEP: /[,\s]+/

kamer:  TK | EK | VV
TK: "II" | "2"
EK: "I" | "1"
VV: "VV"

vergaderjaar: JAAR4 JAARSEP (JAAR4 | JAAR2)
            | JAAR2 JAARSEP JAAR2
JAAR4: ("18" | "19" | "20") /[0-9]{2}/
JAARSEP: /[\/-]/
JAAR2: /'?[0-9]{2}/

dossiernummer: DOSSIERNUMMER_BASIS
DOSSIERNUMMER_BASIS: /[0-9]{2}\s?[0-9]{3}[\s-]?[A-Z]*/ | /21\s?501-\d+/
ONDERNUMMERNR: /nr./

// Workaround because the Visitor didn't seem to pick up on the term-only version
ondernummer: ONDERNUMMER
ONDERNUMMER: /[0-9A-Z]+/

paginaverwijzing: PAGINA_AANDUIDING KSSEP? PAGINANUMMER KSSEP? PAGINA_RANGE KSSEP? PAGINANUMMER
                | PAGINA_AANDUIDING KSSEP? PAGINANUMMER

PAGINA_AANDUIDING: "p." | "pag." | "pagina" | "b." | "blz." | "bladzijde"
PAGINANUMMER: /\d+/
PAGINA_RANGE: "e.v."
            | "ev."
            | ","
            | "-"
            | "/"
            | "â€“"
            | "en"
