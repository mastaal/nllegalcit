// Copyright 2023, Martijn Staal <nllegalcit [at] martijn-staal.nl>
// SPDX-License-Identifier: EUPL-1.2
// Available under the EUPL-1.2, or, at your option, any later version.
// Parser grammar for Kamerstuk citations
// This is essentially a (customized) Lark-translation from the (EUPL-1.2 licensed) Waxeye grammar used by
// KOOP, which can be found here: https://gitlab.com/koop/ld/lx/linkextractor/-/blob/main/src/webapp/links/grammars/oep-parl-docs.waxeye?

// TODO: Support for odd kamernrs and R nummers
kamerstuk: (KSLABEL KSSEP)? kamer (KSSEP vergaderjaar_met_pre)? (KSSEP | "-") dossiernummer KSSEP (ONDERNUMMARAANDUIDING KSSEP?)? ondernummer (KSSEP paginaverwijzing)?

KSLABEL: /kamerstuk(ken)?/i

KSSEP: ("," | WS)+

kamer:  TK | EK | VV
TK: "II"
  | "2"
  | "Tweede" WS "Kamer"
  | "TK"
  | "T.K."
  | "11"
EK: "I"
  | "1"
  | "Eerste" WS "Kamer"
  | "EK"
  | "E.K."
VV: "VV"
  | "Verenigde" WS "Vergadering"
  | "I-II"
  | "I/II"

vergaderjaar_met_pre: (VERGADERJAAR_PRE WS)? vergaderjaar
vergaderjaar: (JAAR4 JAARSEP (JAAR4 | JAAR2) | JAAR2 JAARSEP JAAR2)
VERGADERJAAR_PRE: /vergader.?jaar/i
                | "zitting"i
                | "zittingsjaar"i
JAAR4: ("18" | "19" | "20") /[0-9]{2}/
JAARSEP: "/" | "-" | "/-" WS?
JAAR2: /'?[0-9]{2}/

dossiernummer: DOSSIERNUMMER DOSSIERNUMMER_TOEVOEGING | DOSSIERNUMMER
DOSSIERNUMMER: /[0-9]{2}[-.\s]?[0-9]{2,3}/
DOSSIERNUMMER_TOEVOEGING: /[.\s\-][A-Z]+/
                        | WS "hoofdstuk"i WS /[A-Z]+/
                        | /[-]\d{2}/
                        | /[IVXLCDM]+[A-Z]*/ // Sometimes the toevoeging is added directly to the dossiernummer.

ONDERNUMMARAANDUIDING: "nr."i
                     | "nr"i
                     | "nrs."i
                     | "nrs"i
                     | "nummer"i
                     | "nummers"i

// Workaround because the Visitor didn't seem to pick up on the term-only version
ondernummer: ONDERNUMMER
ONDERNUMMER:  /[1-9][0-9]{0,4}[a-z]?/ | /[A-Z]+/

paginaverwijzing: PAGINA_AANDUIDING KSSEP? (PAGINA_LOS | pagina_range)

PAGINA_AANDUIDING: /p(ag(ina)?)?\.?/i
                 | /b(lz)?\.?/i
                 | "bladzijde"i
PAGINANUMMER: /[0-9]+/
PAGINA_LOS: PAGINANUMMER
PAGINA_START: PAGINANUMMER
PAGINA_EIND: PAGINANUMMER
pagina_range: PAGINA_START KSSEP? PAGINA_RANGE_AANDUIDING KSSEP? PAGINA_EIND
PAGINA_RANGE_AANDUIDING: "e.v."
                       | "e v."
                       | "ev."
                       | ","
                       | "-"
                       | "/"
                       | "–"
                       | "–"
                       | "en"

%import unicode.WS